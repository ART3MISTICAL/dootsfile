export FZF_SHOW_HELP_OPTS="$(
cat <<-EOF

        FZF Keybinds Shortcut

        ?           Toggle/Hide Preview
        C-space     Change preview layout
        C-e         Open in Editor
        C-v         Open in VsCode
        C-o         Launch Application Chooser
        M-o         Open in Default Appllication
        C-/         Directory: Navigate on broot            
        C-/         File: Open in Pager (bat)

        M-s         Toggle Sort
        C-y         Copy/Yank
        C-a         Select all
        C-M-d       Deselect All
        Del         Delete/Remove file

        Alt-?       Help (this page)
        ESC         Quit
EOF
)"

export FZF_THEME_CATPPUCCIN_MOCHA="
--color=bg+:#313244,bg:#1e1e2e,spinner:#f5e0dc,hl:#f38ba8 \
--color=fg:#cdd6f4,header:#f38ba8,info:#cba6f7,pointer:#f5e0dc \
--color=marker:#f5e0dc,fg+:blue,border:blue,prompt:#cba6f7,hl+:#f38ba8"

export FZF_PREVIEW_OPTS="--preview
'([[ {} =~ ('.jpg'|'.jpeg'|'.png'|'.gif'|'.bmp'|'.svg'|'.mp4'|'.mkv')$ ]] && (chafa {} && exiftool {})) || 
([[ -f {} ]] && (bat --style=header,numbers,changes,plain --color=always --language=sh --line-range :500 {} || cat {})) || 
([[ -d {} ]] && (lsd  -all --long --tree --depth=5 --group-dirs=first -I=.git {} )) || echo {} 3>/dev/null | head -n 500'
"

#'([[ \$(file -bL --mime-type {} 2> /dev/null = image) ]] && (catimg -w 100 {})) || # throwing an stb error cant silence

export FZF_PREVIEW_KEYBIND_OPTS="
--bind '?:toggle-preview'
--bind 'alt-?:preview(printf \"${FZF_SHOW_HELP_OPTS}\")'
--bind 'alt-j:preview-down'
--bind 'alt-k:preview-up'
--bind 'ctrl-d:preview-page-down'
--bind 'ctrl-u:preview-page-up'
--bind 'ctrl-t:preview-top'
--bind 'ctrl-b:preview-bottom'
--bind 'ctrl-l:clear-screen+clear-query'
--bind 'ctrl-space:change-preview-window(right,80%,nohidden|down,80%,border-top,nohidden|down,50%,nohidden|up,80%,border-down,nohidden|up,50%,nohidden|left,80%,nohidden|left,50%,nohidden|down:3:nohidden:wrap|up:3,nohidden:wrap|right,50%,nohidden)'
"
export FZF_KEYBIND_SHORTCUTS="
$FZF_PREVIEW_KEYBIND_OPTS
--bind 'alt-o:execute(xdg-open {+})'
--bind 'alt-s:toggle-sort'
--bind 'ctrl-/:execute(
if [[ -d {} ]]; then
  broot {} < /dev/tty > /dev/tty 2>&1
elif [[ \$(file --mime {}) =~ image/ ]]; then
  chafa {} | less > /dev/tty
else
  bat --paging=always --style=plain --color=always --language=sh {} > /dev/tty
fi)'
--bind 'ctrl-a:select-all'
--bind 'ctrl-alt-d:deselect-all'
--bind 'ctrl-o:execute(flatpak run re.sonny.Junction {+})'
--bind 'ctrl-y:execute-silent(wl-copy {+})'
--bind 'ctrl-alt-y:execute-silent(readlink -f {+} | wl-copy)'
--bind 'ctrl-e:execute(${EDITOR} {} > /dev/tty)'
--bind 'ctrl-v:execute(code {+})'
--bind 'del:execute(rm -iv {};)+reload($FZF_DEFAULT_COMMAND)+clear-screen'
"
# --bind 'ctrl-y:execute-silent(printf {} | cut -f 2- | wl-copy --trim-newline'
# --bind 'ctrl-/:execute(if [[ -f {} ]]; then bat --paging=always --style=\"header,numbers,changes\" --language=sh {}  < /dev/tty > /dev/tty 2>&1; else broot {} < /dev/tty > /dev/tty 2>&1
# ; fi)'

export FZF_DEFAULT_COMMAND="fd --color=always --hidden --exclude .git"

export FZF_DEFAULT_OPTS="
"$FZF_PREVIEW_OPTS"
"$FZF_KEYBIND_SHORTCUTS"
"$FZF_THEME_CATPPUCCIN_MOCHA"
-i
--ansi
--multi
--height=90%
--info=inline
--no-separator
--layout=reverse
--preview-window=:hidden
"

export FZF_ALT_C_COMMAND="fd --type=d --color=always --hidden --exclude .git"

export FZF_ALT_C_OPTS="
--preview 'lsd --all --long --tree --depth=3 {} | head -500'
--preview-window 'nohidden,<50(down,75%,border-top)'
--bind 'alt-h:reload(fd --type=d --color=always --follow --exclude .git)'
--bind 'alt-c:reload(fd -p ~ --color=always --hidden --type=d --follow)'
"

export FZF_CTRL_T_COMMAND="$FZF_DEFAULT_COMMAND"

export FZF_CTRL_T_OPTS="
--exit-0
--select-1
--info=default
--layout=reverse-list
--preview-window '50%,<50(up,75%,border-down)'
--header 'Alt-D: Directories | Alt-F: Files | Alt-H: Hide Files'
--bind 'alt-d:change-prompt(  Directories > )+reload("$FZF_ALT_C_COMMAND")'
--bind 'alt-f:change-prompt(  Files > )+reload("$FZF_DEFAULT_COMMAND")'
--bind 'alt-h:change-prompt(  Hide Files > )+reload(fd --type=f --color=always --follow)'
--bind 'ctrl-t:change-prompt(Home > )+reload(fd --base-directory ~ --color=always --hidden --exclude .git)'
"

export FZF_CTRL_R_OPTS="
--preview 'echo {+} | bat --color=always --wrap never --language=sh --style=plain'
--preview-window 'down:3:nohidden:wrap'"

# export FZF_TMUX_OPTS='-p80% --color=border:blue'
# FZF_TMUX_CTRL_R_OPT="fzf-tmux -p $FZF_CTRL_R_OPTS"
# --bind 'alt-p:execute($'FZF_TMUX_OPTS'='-p90% --color=border:blue')'

# fzf completion '**' doesn't preview files (idk if it is a bug)
_fzf_compgen_path() {
  fd --color=always --hidden --follow --exclude ".git" . "$1"
}
_fzf_compgen_dir() {
  fd --color=always --type d --hidden --follow --exclude ".git" . "$1"
}

# use atuin history instead 
fzf-atuin-history-widget() {
  local selected num
  setopt localoptions noglobsubst noposixbuiltins pipefail no_aliases 2> /dev/null
  selected=( $(atuin history list --cmd-only | tac | awk '{ cmd=$0; sub(/^[ \t]*[0-9]+\**[ \t]+/, "", cmd); if (!seen[cmd]++) print $0 }' | bat --color=always --wrap never --language=sh --style=plain | 
    FZF_DEFAULT_OPTS="--height ${FZF_TMUX_HEIGHT:-40%} ${FZF_DEFAULT_OPTS-} --scheme=history --bind=ctrl-r:toggle-sort,ctrl-z:ignore ${FZF_CTRL_R_OPTS-} --query=${(qqq)LBUFFER} +m" $(__fzfcmd)) )
  local ret=$?
  if [ -n "$selected" ]; then
    cmd=$selected[1,-1]
    if [ -n "$cmd" ]; then
      zle vi-fetch-history -n $cmd
    fi
  fi
  zle -U "$cmd"
  zle kill-buffer
  zle reset-prompt
  return $ret
}
zle      -N       fzf-atuin-history-widget
bindkey '^R'      fzf-atuin-history-widget


fzf-man-widget() {
  batman="man {1} | col -bx | bat --language=man --plain --color always --theme=\"Monokai Extended\""
   man -k . | sort \
   | awk -v cyan=$(tput setaf 6) -v blue=$(tput setaf 4) -v res=$(tput sgr0) -v bld=$(tput bold) '{ $1=cyan bld $1; $2=res blue;} 1' \
   | fzf  \
      -q "$LBUFFER" \
      --ansi \
      --tiebreak=begin \
      --prompt=' Man > '  \
      --header="M-r: update mandb | M-t: tl;dr | M-c: cheat.sh | M:m manual " \
      --preview-window '50%,rounded,<50(down,80%,border-up)' \
      --preview "${batman}" \
      --bind "enter:execute(man {1})" \
      --bind "alt-r:execute(sudo mandb)" \
      --bind "alt-c:+change-preview(cht.sh {1})+change-prompt(ﯽ Cheat > )" \
      --bind "alt-m:+change-preview(${batman})+change-prompt( Man > )" \
      --bind "alt-t:+change-preview(tldr --color=always {1})+change-prompt(ﳁ TLDR > )"
  zle reset-prompt
}
zle     -N          fzf-man-widget
bindkey '^[h'       fzf-man-widget

fzf-cd-recent-dir-widget () {
  local dir
  print -rNC1 -- $dirstack |
    fzf --color=fg:bold:blue --read0 --print0 --preview-window 'nohidden,<50(down,75%,border-top)' |
    IFS= read -rd '' dir &&
    BUFFER="builtin cd -- $dir"
    if [[ -n $BUFFER ]]; then
        zle accept-line
    fi
  zle reset-prompt
}

zle -N fzf-cd-recent-dir-widget
bindkey '^[C' fzf-cd-recent-dir-widget #<Alt-Shift-C>

fzf-dictionary-widget() {
local dict wiki wikis gogl
dict="dict {}"
wiki="wiki {} > /dev/tty"
wweb="w3m https://en.wikipedia.org/wiki/{}"
gogl="w3m https://google.com/search?q=define\ {}"
  LBUFFER="$LBUFFER$(FZF_DEFAULT_COMMAND= cat /usr/share/dict/*words | sort | uniq -id | \
    fzf-tmux \
        -p60% \
        --layout=default \
        --header-first \
        --header="M-w: Wiki | M-d: Define | M-g: Google" \
        --color=fg:blue,fg+:blue,border:blue \
        --bind="alt-d:change-preview($dict)" \
        --bind="alt-w:execute($wiki)" \
        --bind="alt-g:execute($gogl)" \
        --prompt=" > " \
        --preview "$dict" \
        --preview-window='up,85%,border-bottom,wrap' | paste -sd" " -)"
  zle reset-prompt
}

zle     -N    fzf-dictionary-widget
bindkey '^[d' fzf-dictionary-widget #<Alt-D>
